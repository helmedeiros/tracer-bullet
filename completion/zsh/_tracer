#compdef tracer

# Autocomplete plugin for Oh-My-Zsh
# Requires: Trace Bullet (http://github.com/helmedeiros/tracer-bullet.git)

local -a _1st_arguments
_1st_arguments=(
  "configure":"optional configurations"
  "commit":"easy commit for configured projects"
  "pair":"Configure pairing informations"
  "story\:by":"list all stories played by #"
  "story\:commits":"list all commits for a story #"
  "story\:files":"list all files modified for a story #"
  "story\:diary":"add to jira as coments the commit from current day for a story #"
  "story\:diff":"diff all changed files for a story #"
  "--version":"show version"
  "--help":"show help"
)

_arguments '*:: :->command'

if (( CURRENT == 1 )); then
  _describe -t commands "command" _1st_arguments
  return
fi

local -a _command_args
case "$words[1]" in
    configure)
    _command_args=(
      '(-a|--autocomplete)'{-a,--autocomplete}'[configure autocomplete]' \
      '(-j|--jira)'{-j,--jira}'[configure jira credentials]' \
      '(-p|--project)'{-p,--project}'[configure project]' \
      '(-u|--user)'{-u,--user}'[configure the current project user]' \
    )
    ;;
  pair)
    _command_args=(
    '(-a|--alone)'{-a,--alone}'[When you are not pairing with no one]' \
    '(-w|--with)'{-w,--with}'[Im pairing with #]' \
    '(-s|--story)'{-s,--story}'[Im pairing on story #]' \
    )
    ;;
  story:files)
    _command_args=(
    '(-mt|--missing-tests)'{-mt,--missing-tests}'[Show missing Tests]' \
    '(-t|--tests)'{-t,--tests}'[Show only Tests]' \
    '(-s|--sql)'{-s,--sql}'[Show only SQLs]' \
    )
    ;;
  story:diary)
    _command_args=(
    '(-t|--today)'{-t,--today}'[Post to jira todays diary]' \
    '(-y|--yesterday)'{-y,--yesterday}'[Post to jira yesterdays diary]' \
    '(-d|--days)'{-d,--days}'[Post to jira # days diary]' \
    )
    ;;

esac

_arguments \
  $_command_args \
  &&  return 0
